plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'org.jetbrains.kotlin.jvm'
}

group = 'net.pixelateddream'
version = '1.0.1-UP'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.21.10-R0.1-SNAPSHOT")
    implementation 'org.java-websocket:Java-WebSocket:1.5.3'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

shadowJar {
    // Relocate dependencies to avoid conflicts with other plugins
    relocate 'org.java_websocket', 'net.pixelateddream.macebattles.lib.websocket'
    relocate 'com.google.gson', 'net.pixelateddream.macebattles.lib.gson'

    // Set the output filename
    archiveClassifier.set('')
}

// Make build task depend on shadowJar
build {
    dependsOn shadowJar
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version, buildNumber: (new File("build.properties").exists() ? new Properties().with { p -> p.load(new FileInputStream("build.properties")); p }["build.number"] : "1")]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

apply from: 'incrementBuild.gradle'
kotlin {
    jvmToolchain(21)
}
